plugins {
	id "org.asciidoctor.convert" version "1.5.2"
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.asciidoctor.convert'

dependencies {
	compile project(':subs-ena-repository')
	testCompile('org.springframework.boot:spring-boot-starter-test')
	compile 'uk.ac.ebi.tsc.aap.client:security:0.2.2-SNAPSHOT'
	testCompile 'org.springframework.restdocs:spring-restdocs-mockmvc'
	compile 'uk.ac.ebi.subs:subs-test-categories:0.1.0-SNAPSHOT'
}

//bootRepackage.enabled = false

springBoot {
	executable = true
}

mainClassName = "uk.ac.ebi.subs.SubsEnaRepositoryApplication"

ext {
	snippetsDir = file('build/generated-snippets')
}

test {
	outputs.dir snippetsDir
}

task (testsMakesDocs, type: Test) {
	outputs.dir snippetsDir
	useJUnit {
		includeCategories 'uk.ac.ebi.subs.DocumentationProducer'
	}
	testLogging {
		exceptionFormat = 'full'
	}
}

configure(testsMakesDocs) {
	group = "documentation"
	description = 'Run tests that generate documentation snippets'
}

asciidoctor {
	attributes 'snippets': snippetsDir
	sourceDir file('src/main/resources/docs')
	backends 'html5'
	inputs.dir snippetsDir
	dependsOn testsMakesDocs
}

jar {
	dependsOn asciidoctor
	from ("${asciidoctor.outputDir}/html5") {
		into 'static/docs'
	}
}

